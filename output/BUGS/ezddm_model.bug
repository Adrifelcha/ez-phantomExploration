
model {
          ##################
          ####### Priors
          ##################                    
          drift_mu ~ dnorm(0,1)                      # Baseline drift rate
          drift_lambda ~ dgamma(2,1)
          drift_sigma = pow(drift_lambda, -0.5)
          bound_mu ~ dnorm(2,1)T(0,)                 # Baseline boundary separation
          bound_lambda ~ dgamma(2,1)
          bound_sigma = pow(bound_lambda, -0.5)
          nondt ~ dexp(1)                            # Participant-specific non-decision time  
                                        
          # Regression coefficients (2 main effects + 1 interaction)
          for(i in 1:3){                           
              gamma[i] ~ dnorm(0,1)                # Drift rate regression coefficients
              lambda[i] ~ dnorm(0,1)               # Boundary separation regression coefficients
          }      
          
          # Condition-specific drift rate and boundary separation
          for(j in 1:4){
              drift_pred[j] = drift_mu + gamma[1]*A[j]+gamma[2]*B[j]+gamma[3]*A[j]*B[j]
              bound_pred[j] = bound_mu + lambda[1]*A[j]+lambda[2]*B[j]+lambda[3]*A[j]*B[j]
          }
          
          #########################
          ####### Sampling model 
          ####### (for each experimental session)
          #########################          
          for (k in 1:length(meanRT)) {
              # Person-by-condition specificDM parameters                                    
              drift[k] ~ dnorm(drift_pred[cond[k]],drift_lambda)
              bound[k] ~ dnorm(bound_pred[cond[k]],bound_lambda)                  
      
              # Forward equations from EZ Diffusion
              ey[k]  = exp(-bound[k] * drift[k])
              Pc[k]  = 1 / (1 + ey[k])
              PRT[k] = 2 * pow(drift[k], 3) / bound[k] * pow(ey[k] + 1, 2) / (2 * -bound[k] * 
                      drift[k] * ey[k] - ey[k] * ey[k] + 1)
              MDT[k] = (bound[k] / (2 * drift[k])) * (1 - ey[k]) / (1 + ey[k])
              MRT[k] = MDT[k] + nondt
      
              # Sampling distributions for summary statistics
              correct[k] ~ dbin(Pc[k], nTrials[k])
              varRT[k]   ~ dnorm(1/PRT[k], 0.5*(nTrials[k]-1) * PRT[k] * PRT[k])
              meanRT[k]  ~ dnorm(MRT[k], PRT[k] * nTrials[k])
            }
}
